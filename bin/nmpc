#!/bin/zsh

MPDSERVER="localhost"
MPDPORT="6600"

DEFCMD="nmpcfunc playing"

alias -g clean='sed "/^OK.*$/d"'

function help()
{
    echo "nmpc - a simple netcat-powered MPD client."
    echo
    echo "Usage: nmpc <command> [<arg1> ... <argn>]"
    echo
    echo "MPD Commands:"
    echo "  These are just sent on to MPD as-is."
    echo
    echo "     Playback Commands"
    echo "         next"
    echo "         previous"
    echo "         play"
    echo "         pause"
    echo "         stop"
    echo "         setvol <int vol>"
    echo
    echo "     Track Commands"
    echo "         find <string type> <string what>"
    echo "         list <metadata arg1> [<metadata arg2> <search term>]"
    echo
    echo "     Playlist Commands"
    echo "         clear"
    echo "         shuffle"
    echo "         add <string>"
    echo "         delete <int song>"
    echo "         save <string playlist name>"
    echo "         rm <string playlist name>"
    echo
    echo "     Database Commands"
    echo "         update"
    echo
    echo "NMPC Commands:"
    echo "  These are more advanced commands handled by nmpc"
    echo "     toggle              - toggle the play/pause state"
    echo "     crossfade           - toggle the crossfade state"
    echo "     repeat              - toggle the repeat state"
    echo "     random              - toggle the random state"
    echo "     raw                 - send \$2 as a command to MPD"
    echo "     info                - information about MPD."
    echo "     playing             - information about the current playlist."
    echo "     playlist <playlist> - clear the playlist and load the specified playlist."
    echo "     playlists           - list the playlists."
}

function line()
{
    echo "$1" | grep -i "$2: " | sed "s/$2: //i"
}

function toggle()
{
    if mpdfunc "$1" | grep -q "$2"; then
        rawcmd "$3" &>/dev/null
    else
        rawcmd "$4" &>/dev/null
    fi
}

function nicetime()
{
    printf "%02d days %02d:%02d:%02d\n" $[$1 / 3600 / 24 ] $[$1 / 3600 % 24 ] $[$1 / 60 % 60] $[$1 % 60]
}

function rawcmd()
{
    echo "$1\nclose" | netcat $MPDSERVER $MPDPORT
}

function mpdfunc()
{
    arglist=$1

    [ "$2" != "" ] &&                   arglist="$arglist $2"
    [ "$3" != "" ] && [ "$4" = "" ]  && arglist="$arglist \"$3\""
    [ "$3" != "" ] && [ "$4" != "" ] && arglist="$arglist $3 \"$4\""

    [ "$1" = "add" ] || [ "$1" = "save" ] || [ "$1" = "rm" ] && arglist="$1 \"$2\""

    rawcmd "$arglist" | clean
}

function nmpcfunc()
{
    case $1 in
        "toggle")
            toggle status "state: play" pause play;;

        "crossfade")
            toggle status "xfade: 0" "crossfade 5" "crossfade 0";;

        "repeat")
            toggle status "repeat: 0" "repeat 1" "repeat 0";;

        "random")
            toggle status "random: 0" "random 1" "random 0";;

        "raw")
            rawcmd "$2";;

        "info")
            info=`rawcmd stats | clean`

            artists=`line $info artists`
            albums=`line $info albums`
            songs=`line $info songs`

            uptime=`nicetime \`line $info uptime\``
            playtime=`nicetime \`line $info playtime\``

            dbupdate=`date -d \`line $info updated\` -R`
            dbplaytime=`nicetime \`line $info db_playtime\``

            echo "$artists artists, $albums albums, $songs songs."
            echo
            echo "Uptime:    $uptime"
            echo "Play time: $playtime"
            echo
            echo "Database Updated:  $dbupdate"
            echo "Database Playtime: $dbplaytime";;

        "playing")
            info=`rawcmd status | clean`
            sinfo=`rawcmd currentsong | clean`

            track=`line $sinfo title`
            artist=`line $sinfo artist`
            album=`line $sinfo album`
            state=`line $info state`

            volume=`line $info volume`
            repeat="on";    [ "`line $info repeat`" = "0" ] && repeat="off"
            random="on";    [ "`line $info random`" = "0" ] && random="off"
            crossfade="on"; [ "`line $info xfade`"  = "0" ] && crossfade="off"
            
            echo "$track, by $artist in $album [$state]"
            echo "$volume% volume, repeat $repeat, random $random, crossfade $crossfade";;

        "playlist")
            rawcmd clear | clean
            rawcmd "playlist \"$2\"" | clean;;

        "playlists")
            info=`rawcmd lsinfo | clean`
            echo $info | grep "playlist: " | sed "s/playlist: //";;
        esac
}

function error()
{
    echo "Unknown command: $*"
}

case $1 in
    "-h" | "--help")
        help;;

    "clear" | "next" | "previous" | "play" | "pause" | "stop" | "update" | "find" | "list" | "add" | "delete" | "save" | "rm" | "shuffle" | "setvol")
        mpdfunc $*;;

    "toggle" | "crossfade" | "repeat" | "random" | "raw" | "info" | "playing" | "playlist" | "playlists")
        nmpcfunc $*;;

    "")
        $DEFCMD[(w)1] $DEFCMD[(w)2] $DEFCMD[(w)3] $DEFCMD[(w)4] $DEFCMD[(w)5];;

    *)
        error $*;;
esac
